buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.github.maiflai:gradle-scalatest:0.18'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0'
        classpath 'net.researchgate:gradle-release:2.6.0'
    }
}

apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'net.researchgate.release'

allprojects {
    group = 'com.landoop'
    version = version
    description = "SQL Kafka Connect SMT library"

    apply plugin: 'scala'
    apply plugin: 'maven'
    apply plugin: 'com.github.maiflai.scalatest'
    apply plugin: 'distribution'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        scalaMajorVersion = '2.11'
        scala = '2.11.8'
        scalaCheck = '1.11.1'
        scalaTest = '2.2.6'
        junitVersion = '4.12'
        kafkaVersion = '1.0.0'
        confluentVersion = '4.0.0'
        scalaLoggingVersion = '2.1.2'
        mockitoVersion = '2.7.13'
        avroVersion = '1.8.1'
        avro4sVersion = "1.6.2"
        slf4jVersion = "1.7.7"
        jsonSql =  "1.0.1"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://packages.confluent.io/maven/" }
        maven { url "http://repo.typesafe.com/typesafe/releases/" }
    }

    configurations {
        provided
        compile.extendsFrom provided
    }

    dependencies {
        compile "org.scala-lang:scala-library:$scala"
        compile "com.typesafe.scala-logging:scala-logging-slf4j_$scalaMajorVersion:$scalaLoggingVersion"
        compile("com.landoop:json-sql_$scalaMajorVersion:$jsonSql")

        provided "org.apache.kafka:connect-api:${kafkaVersion}"
        testCompile "org.mockito:mockito-core:$mockitoVersion"
        testCompile "org.scalacheck:scalacheck_$scalaMajorVersion:$scalaCheck"
        testCompile "org.scalatest:scalatest_$scalaMajorVersion:$scalaTest"
        testCompile "io.confluent:kafka-connect-avro-converter:$confluentVersion"
        testCompile "junit:junit:$junitVersion"
        testCompile "org.apache.curator:curator-test:3.1.0"
        testCompile 'org.powermock:powermock-module-junit4:1.6.5'
        testCompile 'org.pegdown:pegdown:1.1.0'
        testCompile "org.apache.avro:avro:$avroVersion"
        testCompile "com.sksamuel.avro4s:avro4s-core_$scalaMajorVersion:$avro4sVersion"
    }

    test {
        maxParallelForks = 1
        minHeapSize '256m'
        maxHeapSize '2048m'
        systemProperty 'keystore', projectDir.canonicalPath + "/src/test/resources/stc_keystore.jks"
        systemProperty 'truststore', projectDir.canonicalPath + "/src/test/resources/stc_truststore.jks"
    }

    task testJar(type: Jar, dependsOn: testClasses) {
        baseName = "test-${project.archivesBaseName}"
        from sourceSets.test.output
    }

    configurations {
        tests
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task scaladocJar(type: Jar) {
        classifier = 'scaladoc'
        from '../LICENSE'
        from scaladoc
    }

    tasks.withType(Tar) {
        compression Compression.GZIP
        extension = 'tgz'
    }

    distributions {
        main {
            contents {
                from jar
                from(project.configurations.compile)
            }
        }
    }

    artifacts {
        archives javadocJar, scaladocJar, sourcesJar
    }

    task compile(dependsOn: 'compileScala')
    javadoc.dependsOn scaladoc

    signing {
        required { gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    // OSSRH publication
    if (project.hasProperty('release')) {
        uploadArchives {
            repositories {
                mavenDeployer {
                    // POM signature
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    // Target repository
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }
                    pom.project {
                        name project.name
                        description project.description
                        packaging 'jar'
                        url 'https://github.com/landoop/kafka-connect-kcql-smt'

                        scm {
                            connection 'scm:git:https://github.com/landoop/kafka-connect-kcql-smt.git'
                            developerConnection 'scm:git:git@github.com:landoop/kafka-connect-kcql-smt.git'
                            url 'https://github.com/landoop/kafka-connect-kcql-smt.git'
                        }

                        licenses {
                            license {
                                name 'Apache License 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.html'
                                distribution 'repo'
                            }
                        }

                        developers {

                            developer {
                                id = 'stheppi'
                                name = 'Stefan Bocutiu'
                                email = 'stefan@landoop.com'
                            }
                            developer {
                                id = 'Antwnis'
                                name = 'Antonios Chalkiopoulos'
                                email = 'antonios@landoop.com'
                            }
                        }
                    }
                }
            }
        }

        nexusStaging {
            packageGroup = project.getGroup()
            username = ossrhUsername
            password = ossrhPassword
        }
    }
}

